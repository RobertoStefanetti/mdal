buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:2.0.8'
	}
}

allprojects {
	apply plugin: 'jacoco'
	
	repositories {
		mavenCentral()
	}
}

subprojects {
	ext.xtextVersion = '2.21.0'
	repositories {
		mavenCentral()
	}
	
	apply plugin: 'java-library'
	dependencies {
		implementation platform("org.eclipse.xtext:xtext-dev-bom:${xtextVersion}")
	}

	apply plugin: 'org.xtext.xtend'
	apply from: "${rootDir}/gradle/source-layout.gradle"
	apply plugin: 'eclipse'
	
	group = 'de.joneug.mdal'
	version = '0.1.0'
	
	sourceCompatibility = '11'
	targetCompatibility = '11'
	
	configurations.all {
		exclude group: 'asm'
	}
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/coverage.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}
